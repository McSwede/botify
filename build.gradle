plugins {
    id "java"
    id "org.springframework.boot" version "2.2.1.RELEASE"
    id "io.spring.dependency-management" version "1.0.8.RELEASE"
    id "org.liquibase.gradle" version "2.0.1"
}

repositories {
    jcenter()
    mavenCentral()

    maven {
        url = "https://jitpack.io"
    }

    maven {
        url = "https://dl.bintray.com/sedmelluq/com.sedmelluq"
    }

    maven {
        url = "https://repo.jenkins-ci.org/releases/"
    }
}

compileJava.options.encoding = "UTF-8"

dependencies {
    implementation "ch.qos.logback:logback-classic:1.2.3"

    implementation "com.antkorwin:xsync:1.3"
    implementation "com.google.apis:google-api-services-youtube:v3-rev20200518-1.30.9"
    implementation "com.google.guava:guava:29.0-jre"
    implementation "com.sedmelluq:lavaplayer:1.3.61"
    implementation "com.sedmelluq:lavaplayer-ext-youtube-rotator:0.2.3"

    implementation "commons-validator:commons-validator:1.7"

    implementation "net.dv8tion:JDA:4.2.0_222"
    implementation "net.robinfriedli:exec:1.1"
    implementation "net.robinfriedli:JXP:2.0.3"
    implementation "net.robinfriedli:StringList:1.6"

    implementation "org.apache.commons:commons-collections4:4.4"
    implementation "org.apache.commons:commons-text:1.9"
    implementation "org.codehaus.groovy:groovy-all:3.0.5"
    implementation "org.discordbots:DBL-Java-Library:2.0.1"
    implementation "org.ehcache:ehcache:3.9.0"
    implementation "org.glassfish:javax.el:3.0.1-b11"
    implementation "org.hibernate:hibernate-c3p0:5.4.21.Final"
    implementation "org.hibernate:hibernate-core:5.4.21.Final"
    implementation "org.hibernate:hibernate-entitymanager:5.4.21.Final"
    implementation "org.hibernate:hibernate-jcache:5.4.21.Final"
    implementation "org.hibernate:hibernate-validator:5.4.3.Final"
    implementation "org.hibernate:hibernate-validator-cdi:5.4.3.Final"
    implementation "org.kohsuke:groovy-sandbox:1.25"
    implementation("org.liquibase:liquibase-core:4.1.1") {
        exclude group: "ch.qos.logback"
    }
    liquibaseRuntime "org.liquibase.ext:liquibase-hibernate5:3.9.0"
    implementation "org.postgresql:postgresql:42.2.16"
    implementation "org.quartz-scheduler:quartz:2.3.2"
    implementation "org.springframework.boot:spring-boot-dependencies:2.3.4.RELEASE"
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-jdbc"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework:spring-core:5.2.9.RELEASE"

    // check if IPlaylistItem implementations were added when upgrading and update SpotifyTrack accordingly
    implementation "se.michaelthelin.spotify:spotify-web-api-java:6.3.0"

    liquibaseRuntime sourceSets.main.runtimeClasspath
    liquibaseRuntime sourceSets.main.output
}

group = "botify"
version = "1.0-SNAPSHOT"
description = "botify"
sourceCompatibility = "11"
targetCompatibility = "11"

jar {
    manifest {
        attributes(
            "Main-Class": "net.robinfriedli.botify.boot.SpringBootstrap"
        )
    }
}

sourceSets {
    main {
        resources {
            srcDirs "src/main/resources", "src/main/webapp"
        }
    }
}

def applicationProperties = new Properties()
file("src/main/resources/application.properties").withInputStream { applicationProperties.load(it) }
def privateProperties = new Properties()
file("src/main/resources/settings-private.properties").withInputStream { privateProperties.load(it) }

bootRun {
    jvmArgs("-Xmx" + applicationProperties.getProperty("botify.preferences.maxHeapSize"))
}

liquibase {
    activities {
        main {
            changeLogFile applicationProperties.getProperty("liquibase.change-log-path")
            referenceUrl applicationProperties.getProperty("liquibase.referenceUrl")
            url applicationProperties.getProperty("spring.datasource.url")
            username privateProperties.getProperty("spring.datasource.username")
            password privateProperties.getProperty("spring.datasource.password")
            referenceDriver "liquibase.ext.hibernate.database.connection.HibernateDriver"
        }
    }
}